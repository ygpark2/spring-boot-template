buildscript {
    ext {
        springBootVersion = '1.2.6.RELEASE'
        springLoadedVersion = "1.2.0.RELEASE"
        queryDSLVersion = "3.6.7"
        hsqldbVersion = "2.3.2"
        mysqldbVersion = "5.1.36"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
// apply plugin: 'application'

archivesBaseName = 'CSOSEngine'
version = '1.0-SNAPSHOT'

jar {
    baseName = 'ain-spring-boot'
    version =  '0.1.0'
    manifest {
        attributes 'Implementation-Title': 'Spring Boot Template Project',
                'Implementation-Version': version
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs "libs"
    }
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    querydslapt
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter") {
        exclude module: "spring-boot-starter-logging"
    }
    // tag::web[]
    compile "org.springframework.boot:spring-boot-starter-web"
    // tag::amqp[]
    compile "org.springframework.boot:spring-boot-starter-amqp"
    // tag::jdbc[]
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    // tag::log4j[]
    compile "org.springframework.boot:spring-boot-starter-log4j2"
    // tag::actuator[]
    compile "org.springframework.boot:spring-boot-starter-actuator"
    // tag::data-jpa[]
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    // compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'

    // compile "com.mysema.querydsl:querydsl-core:$queryDslVersion"
    compile "com.mysema.querydsl:querydsl-jpa:${queryDSLVersion}"
    querydslapt "com.mysema.querydsl:querydsl-apt:${queryDSLVersion}"

    runtime "org.hsqldb:hsqldb:${hsqldbVersion}"

    compile "org.springframework:spring-jms"
    compile "org.apache.activemq:activemq-broker"

    compile group: "de.codecentric", name: "spring-boot-admin-server", version: "1.2.3"
    compile group: "de.codecentric", name: "spring-boot-admin-server-ui", version: "1.2.3"

    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    // compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    // compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'org.apache.commons', name: 'commons-pool2', version: '2.4.1'
    compile group: 'geronimo-spec', name: 'geronimo-spec-j2ee-management', version: '1.0-M1'
    compile group: 'org.apache.geronimo.specs', name: 'geronimo-jms_1.1_spec', version: '1.1.1'
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-client', version: '3.1.3'
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-common', version: '3.1.3'
    compile group: 'org.apache.xmlrpc', name: 'xmlrpc-server', version: '3.1.3'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1'
    // compile group: 'org.mybatis.scripting', name: 'mybatis-freemarker', version: '1.1.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: "${mysqldbVersion}"
    // compile group: 'org.springframework', name: 'spring-jms', version: '4.1.5'
    // compile group: 'org.apache.activemq', name: 'activemq-client', version: '5.12.0'
    // compile group: 'org.apache.activemq', name: 'activemq-client', version: '5.11.1'
    compile group: 'com.zaxxer', name: 'HikariCP-java6', version: '2.3.9'
    compile group: 'org.mybatis', name: 'mybatis', version: '3.3.0'
    compile group: 'org.mybatis', name: 'mybatis-cdi', version: '1.0.0-beta2'
    compile group: 'org.mybatis', name: 'mybatis-spring', version: '1.2.3'

    compile files("${System.properties['java.home']}/../lib/tools.jar")

    testCompile ("org.springframework.boot:spring-boot-starter-test") {
        exclude module: "spring-boot-starter-logging"
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

configurations {
    compile.exclude group:'ch.qos.logback'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

applicationDefaultJvmArgs = ["-Dgreeting.language=en"]

mainClassName = "com.ain.engine.ApplicationLoader"

apply plugin: 'docker'

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}